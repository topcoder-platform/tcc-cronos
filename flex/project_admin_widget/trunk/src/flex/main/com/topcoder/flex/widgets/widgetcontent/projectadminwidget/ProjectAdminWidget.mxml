<?xml version="1.0" encoding="utf-8"?>
<!--
     Copyright (C) 2009 TopCoder Inc., All Rights Reserved.
-->

<!--

     This mxml component is the main entry point for the project admin widget.

     @author snow01
     @since Cockpit Share Submission Integration
-->
<tc:ProjectAdminWidgetCodeBehind xmlns:mx="http://www.adobe.com/2006/mxml"
                                 minWidth="400"
                                 width="100%"
                                 minHeight="300"
                                 height="100%"
                                 xmlns:tc="com.topcoder.flex.widgets.widgetcontent.projectadminwidget.*"
                                 xmlns:com="com.topcoder.flex.widgets.widgetcontent.projectadminwidget.com.*"
                                 creationComplete="init()">
    <mx:Style source="./Style.css"/>
    <mx:Script>
        <![CDATA[
        	import mx.events.CollectionEvent;
        	import mx.core.Container;
            import mx.utils.ObjectUtil;
            import mx.collections.Sort;
            import com.topcoder.flex.widgets.widgetcontent.projectadminwidget.qs.vo.Contest;
            import com.topcoder.flex.widgets.widgetcontent.projectadminwidget.qs.vo.Project;
            import com.topcoder.flex.widgets.widgetcontent.projectadminwidget.qs.vo.User;
            import mx.collections.ArrayCollection;
            import mx.rpc.AbstractOperation;
            import com.topcoder.flex.Helper;
            import mx.rpc.soap.SOAPHeader;
            import mx.rpc.events.ResultEvent;
            import com.topcoder.flex.widgets.widgetcontent.projectadminwidget.com.renderer.DGHeaderSkin;
            import mx.core.UIComponent;
            import mx.core.Application;
            import com.topcoder.flex.widgets.widgetcontent.projectadminwidget.qs.model.Model;

            /**
             * Variable set to true if the app is running in local testing mode.
             * otherwisein production mode it should be set to false.
             */
            private var _isLocalTesting:Boolean=false;

            /**
             * Reference to the blazeds end point url. This gets loaded from application parameters.
             */
            [Bindable]
            private var _blazedsendpoint:String=Application.application.parameters.blazedsendpoint;

            /**
             * Reference to the contest service facade url. This gets loaded from application parameters.
             */
            [Bindable]
            private var _contestServiceFacadeWsdl:String=_isLocalTesting ? "http://127.0.0.1:8080/contestfacade/ContestServiceFacadeBean?wsdl" : Application.application.parameters.contestServiceFacadeWsdl;

            /**
             * Current selected column index.
             */
            [Bindable]
            public var columnIndx:int;

            /**
             * Current selected row index.
             */
            [Bindable]
            public var rowIndx:int;

            /**
             * Left mask for shutter animation.
             */
            private var masks1:UIComponent=new UIComponent;

            /**
             * Right mask for shutter animation.
             */
            private var masks2:UIComponent=new UIComponent;

            /**
             * Gets the password from remote service.
             */
            private function getDataService():void {
                if (_isLocalTesting) {
                    initComponent();
                } else {
                    dataservice.getPassword();
                }
            }

            /**
             * Handler for password retrieval.
             */
            private function processPassword(e:ResultEvent):void {
                if (e != null && e.result != null) {
                    this.password=e.result.toString();
                }

                // initialize component.
                initComponent();
            }

            /**
             * Initializes various components and loads data from webservices.
             */
            private function initComponent():void {
                trace("----------------------- initComponent");

                var header:SOAPHeader=getHeader(username, password);
                this.contestServiceFacadeWS.clearHeaders();
                this.contestServiceFacadeWS.addHeader(header);
                /*
                var getPermissionsByUser:AbstractOperation=this.contestServiceFacadeWS.getOperation("getPermissionsByUser");
                
                if (getPermissionsByUser) {
                    trace("Sending userid: " + this.userid + ", " + this.contestServiceFacadeWS.wsdl);
                    getPermissionsByUser.send(this.userid);
                }
                */
                var getSimpleProjectPermissionDataForUser:AbstractOperation=this.contestServiceFacadeWS.getOperation("getSimpleProjectPermissionDataForUser");
                
                if (getSimpleProjectPermissionDataForUser) {
                    trace("Sending userid: " + this.userid + ", " + this.contestServiceFacadeWS.wsdl);
                    getSimpleProjectPermissionDataForUser.send(this.userid);
                }
            }

            /**
             * Initialization function that gets called on widget creation complete.
             */
            private function init():void {
                // assigns itself to various child viewers.
                searchBar.parentWidget=this;
                normalViewer.parentWidget=this;
                searchResultViewer.parentWidget=this;
                userViewer.parentWidget=this;

                if (_isLocalTesting) {
                    this.username="user";
                    this.password="password";
                    this.userid=132458;
                }

                trace("--------------- Project Admin Widget:: username: " + this.username);
                trace("--------------- Project Admin Widget:: userid: " + this.userid);
                trace("--------------- Project Admin Widget:: password: " + this.password);

                model.user=this.username;

                // initialize webservices.
                var header:SOAPHeader=getHeader(username, password);
                ContestServiceFacadeBean.clearHeaders();
                ContestServiceFacadeBean.addHeader(header);
                this.contestServiceFacadeWS=ContestServiceFacadeBean;

                getDataService();
            }

            /**
             * Selects the view in animated way as per the view index.
             *
             * @value view index.
             */
            public function set selectedIndex(value:int):void {
                if (initialized) {
                    if (stack.width > 0 && stack.height > 0) {
                        if (!masks1.parent) {
                            viewContainer.addChildAt(masks1, 1);
                        }
                        if (!masks2.parent) {
                            viewContainer.addChildAt(masks2, 2);
                        }
                        var bmd1:BitmapData=new BitmapData(stack.width / 2, stack.height);
                        bmd1.draw(stack);
                        var bm1:Bitmap=new Bitmap(bmd1);
                        if (masks1.numChildren > 0) {
                            masks1.removeChildAt(0);
                        }
                        masks1.addChild(bm1);
                        masks1.visible=true;
                        masks2.visible=true;

                        var bmd2:BitmapData=new BitmapData(stack.width / 2, stack.height);
                        var m:Matrix=new Matrix();
                        m.tx=-stack.width / 2;
                        bmd2.draw(stack, m);
                        var bm2:Bitmap=new Bitmap(bmd2);
                        bm2.x=stack.width / 2;

                        if (masks2.numChildren > 0) {
                            masks2.removeChildAt(0);
                        }
                        masks2.addChild(bm2);

                        move1.target=masks1;
                        move2.target=masks2;
                        moves.play();

                            //move2.play();
                    }
                    stack.selectedIndex=value;
                }
            }

            /**
             * Animation effect-end handler.
             */
            private function handleEffectEnd():void {
                if (masks1) {
                    masks1.visible=false;
                    masks1.includeInLayout=false;
                }
                if (masks2) {
                    masks2.visible=false;
                    masks2.includeInLayout=false;
                }
            }

            /**
             * Count of to be retrieved contest by project.
             */
            private var _tobeRetrievedContestByProjectCount:int=0;

            /**
             * Count of to be retrieved permissions.
             */
            private var _tobeRetrievedPermissionCount:int=0;

            /**
             * Project ids.
             */
            private var _myProjectIds:Dictionary=new Dictionary();

            /**
             * Project ids to permissions.
             */
            private var _projectIdsToPermission:Dictionary=new Dictionary();

            /**
             * Project ids to contests.
             */
            private var _projectIdsToContests:Dictionary=new Dictionary();

            /**
             * All available user ids.
             */
            private var _userIds:Dictionary=new Dictionary();
            
            /**
             * All available project ids and contestIds.
             */
            private var _pcIds:Dictionary=null;
            
            private var _modelUserIdx:int=0;

            /**
             * Webservice result event handler for getPermissionsByUser
             */
             /*
            private function handlePermissionsByUser(e:ResultEvent):void {
                if (e && e.result && e.result is ArrayCollection) {
                    var permList:ArrayCollection=e.result as ArrayCollection;
                    for each (var perm:Object in permList) {
                        // if current user has full permission on project,
                        // then select the project.
                        if (perm.permissionType && perm.permissionType.name == "project_full") {
                            var projectId:Number=perm.projectId as Number;
                            trace("------------------ ProjectAdmin:: user has full permission for : " + projectId);
                            _myProjectIds[perm.projectId]=perm.projectName;
                            _tobeRetrievedPermissionCount++;
                            _tobeRetrievedContestByProjectCount++;

                            // load all permissions for the project. 
                            loadContestsByProject(projectId);
                            loadPermissionsByProjectOrContest(projectId);
                        }
                    }
                }
            }*/

            /**
             * Loads the permission by give project / contest id from facade.
             *
             * @param id id of the project / contest.
             */
            private function loadPermissionsByProjectOrContest(id:Number):void {
                var header:SOAPHeader=getHeader(username, password);
                this.contestServiceFacadeWS.clearHeaders();
                this.contestServiceFacadeWS.addHeader(header);
                var getPermissionsByProject:AbstractOperation=this.contestServiceFacadeWS.getOperation("getPermissionsByProject");
                if (getPermissionsByProject) {
                    //trace("------------------ ProjectAdmin:: loading permission for project/contest: " + id);
                    getPermissionsByProject.send(id);
                }
            }


            /**
             * Webservice result event handler for getPermissionsByProject
             *
             * @param e webservice result event.
             */
            private function handlePermissionsByProject(e:ResultEvent):void {
                _tobeRetrievedPermissionCount--;

                if (e && e.result && e.result is ArrayCollection) {
                    var permList:ArrayCollection=e.result as ArrayCollection;
                    for each (var perm:Object in permList) {
                        // store permission by project id.
                        if (!_projectIdsToPermission[perm.projectId]) {
                            _projectIdsToPermission[perm.projectId]=new Dictionary();
                        }

                        //trace("------------------ ProjectAdmin:: adding permission for project: " + perm.projectId + ", user: " + perm.userId + ", access: " + perm.permissionType.name);
                        _projectIdsToPermission[perm.projectId][perm.userId]=perm;

                        // store user ids.
                        _userIds[perm.userId]=perm.userHandle;
                    }
                }

                //trace("------------------ COUNT _tobeRetrievedPermissionCount: " + _tobeRetrievedPermissionCount + ", _tobeRetrievedContestByProjectCount: " + _tobeRetrievedContestByProjectCount);
                if (_tobeRetrievedPermissionCount <= 0 && _tobeRetrievedContestByProjectCount <= 0) {
                    trace("------------------ COUNT IS ZERO");
                    // populate model data structure.
                    var userList:ArrayCollection=new ArrayCollection();
                    for (var u:Object in _userIds) {
                        var user:User=new User();
                        user.id=u as Number;
                        user.name=_userIds[u];

                        userList.addItem(user);

                        //trace("------------------ ProjectAdmin:: created user: " + user.id);

                        for (var pid:Object in _myProjectIds) {
                            var proj:Project=new Project();
                            proj.id=pid as Number;
                            proj.name=_myProjectIds[pid];
                            if (_projectIdsToPermission[pid]) {
                                if (_projectIdsToPermission[pid][user.id]) {
                                    var permission:Object=_projectIdsToPermission[pid][user.id];
                                    proj.persistedPermissionId=permission.permissionId;
                                    var permissionType:String=permission.permissionType.name;
                                    if (permissionType == "project_full") {
                                        proj.access="Full";
                                        proj.persistedAccess="Full";
                                    } else if (permissionType == "project_write") {
                                        proj.access="Write";
                                        proj.persistedAccess="Write";
                                    } else if (permissionType == "project_read") {
                                        proj.access="Read";
                                        proj.persistedAccess="Read";
                                    } else {
                                        proj.access="";
                                        proj.persistedAccess="";
                                    }
                                }
                            } else {
                                proj.access="";
                                proj.persistedAccess="";
                                proj.persistedPermissionId=0;
                            }

                            //trace("------------------ ProjectAdmin:: created project: " + proj.id + " and access: " + proj.access);
                            proj.user=user;
                            user.projects.addItem(proj);

                            for each (var c:Object in _projectIdsToContests[pid]) {
                                var contest:Contest=new Contest();
                                contest.id=c.contestId;
                                contest.name=c.name;

                                if (_projectIdsToPermission[contest.id]) {
                                    if (_projectIdsToPermission[contest.id][user.id]) {
                                        var cpermission:Object=_projectIdsToPermission[contest.id][user.id];
                                        contest.persistedPermissionId=cpermission.permissionId;
                                        var cpermissionType:String=cpermission.permissionType.name;
                                        if (cpermissionType == "contest_full") {
                                            contest.access="Full";
                                            contest.persistedAccess="Full";
                                        } else if (cpermissionType == "contest_write") {
                                            contest.access="Write";
                                            contest.persistedAccess="Write";
                                        } else if (cpermissionType == "contest_read") {
                                            contest.access="Read";
                                            contest.persistedAccess="Read";
                                        } else {
                                            contest.access="";
                                            contest.persistedAccess="";
                                        }
                                    }
                                } else {
                                    contest.access="";
                                    contest.persistedAccess="";
                                    contest.persistedPermissionId=0;
                                }

                                //trace("------------------ ProjectAdmin:: created contest: " + contest.id + " for project: " + proj.id + " and access: " + contest.access);
                                contest.project=proj;
                                proj.contests.addItem(contest);
                            }
                            
                            sortByName(proj.contests);
                        }
                        
                        sortByName(user.projects);
                    }

                    // set the model's user list.
                    //trace("------------------ ProjectAdmin:: setting the model's user list");
                    sortByName(userList);
                    model.userList=new ArrayCollection(userList.source.slice(0, 10));
                    sortByName(model.userList);
                    model.userList.filterFunction=model.doFilter;
                }
            }
            
            /**
             * Sorts the given collection by name
             *
             * @param coll specified array collection to be sorted.
             */
            private function sortByName(coll:ArrayCollection):void {
                if (!coll.sort) {
                    var sort:Sort=new Sort();
                    sort.compareFunction=compareName;
                    coll.sort=sort;
                }
                
                coll.refresh();
            }

            /**
             * Callback function on sorting by name.
             */
            private function compareName(a:Object, b:Object, fields:Array=null):int {
                
                return ObjectUtil.stringCompare(a.name, b.name, true);
            }

            /**
             * Callback function on sorting by projectId.
             */
            private function compareprojectId(a:Object, b:Object, fields:Array=null):int {
                
                return ObjectUtil.compare(a.projectId, b.projectId);
            }
            
            
            private function handleSimpleProjectPermissionDataForUser(event:ResultEvent):void
            {
            	if(event && event.result && event.result is ArrayCollection)
            	{
            		if(_pcIds==null){
            			getProjectList(event.result as ArrayCollection);
            		}
            		else
            		{
            			var projects:ArrayCollection=getProjectList(event.result as ArrayCollection);
            			var u:User=model.userList.getItemAt(_modelUserIdx) as User;
            			for each(var p:Project in projects)
            			{
            				p.user=u;
            			}
            			u.projects=projects;
            			
            			this._modelUserIdx++;
            			if(this._modelUserIdx<this.model.userList.length)
            			{
            				var getSimpleProjectPermissionDataForUser:AbstractOperation=this.contestServiceFacadeWS.getOperation("getSimpleProjectPermissionDataForUser");
                
                			if (getSimpleProjectPermissionDataForUser) {
                				getSimpleProjectPermissionDataForUser.send(model.userList.getItemAt(_modelUserIdx).id);
                            }
            			}
            			else
            			{
            				model.userList.filterFunction=model.doFilter;
                            model.doSearch();
                        }
            		}
            	}
            }
            
            public function searchUser(key:String):void
            {
            	var searchUserOp:AbstractOperation=this.contestServiceFacadeWS.getOperation("searchUser");
                
                if (searchUserOp) {
                    searchUserOp.send(key);
                }
            }
            
            private function handleSearchUser(event:ResultEvent):void
            {
            	if(event && event.result && event.result is ArrayCollection)
            	{
            		var _userIds:ArrayCollection=event.result as ArrayCollection;
            		model.userList=new ArrayCollection();
                    for each (var u:Object in _userIds) {
                        var user:User=new User();
                        user.id=u.userId;
                        user.name=u.handle;

                        model.userList.addItem(user);
                    }
                    if(model.userList.length>0){
                    	var getSimpleProjectPermissionDataForUser:AbstractOperation=this.contestServiceFacadeWS.getOperation("getSimpleProjectPermissionDataForUser");
                        _modelUserIdx=0;
                		if (getSimpleProjectPermissionDataForUser) {
                			getSimpleProjectPermissionDataForUser.send(model.userList.getItemAt(0).id);
                        }
                    }
                    else
                    {
                        model.userList.filterFunction=model.doFilter;
                        model.doSearch();
                    }
                    //model.userList.filterFunction=model.doFilter;
                    //model.doSearch();
            	}
            }
            
            private function getProjectList(result:ArrayCollection):ArrayCollection
            {
            	var sort:Sort=new Sort();
                sort.compareFunction=compareprojectId;
                result.sort=sort;
                
                result.refresh();
                
                var first:Boolean=false;
                if(_pcIds==null)
                {
                	first=true;	
                	_pcIds=new  Dictionary();
                }
                var ret:ArrayCollection=new ArrayCollection();
                for(var i:int=0;i<result.length;)
                {
                	var item:Object=result.getItemAt(i);
                	var p:Project=new Project();
                	
                	p.id=item.projectId;
                	p.name=item.pname;
                	
                	if (item.pfull>0) {
                           p.access="Full";
                           p.persistedAccess="Full";
                    } else if (item.pwrite>0) {
                           p.access="Write";
                           p.persistedAccess="Write";
                     } else if (item.pread>0) {
                           p.access="Read";
                           p.persistedAccess="Read";
                     } else {
                           p.access="";
                           p.persistedAccess="";
                     }
                     var contests:ArrayCollection=new ArrayCollection();
                     for(var j=i;j<result.length && result.getItemAt(j).projectId==p.id;j++)
                     {
                     	var c:Contest=new Contest();
                     	var it=result.getItemAt(j);
                     	c.id=it.contestId;
                     	c.name=it.cname;
                     	if (it.cfull>0) {
                           c.access="Full";
                           c.persistedAccess="Full";
                    	} else if (it.cwrite>0) {
                           c.access="Write";
                           c.persistedAccess="Write";
                     	} else if (it.cread>0) {
                           c.access="Read";
                           c.persistedAccess="Read";
                     	} else {
                           c.access="";
                           c.persistedAccess="";
                     	}
                     	c.project=p;
                     	if(first && c.access=="Full"){
                     	    contests.addItem(c);
                     	    _pcIds[c.id]="c"
                     	}
                     	else if(!first && _pcIds[c.id]=="c"){
                     	    contests.addItem(c);
                     	}
                     }
                     p.contests=contests;
                     i=j;
                     if(first && p.access=="Full"){
                     	    ret.addItem(p);
                     	    _pcIds[p.id]="p";
                     }
                     else if(!first && _pcIds[p.id]=="p"){
                     	    ret.addItem(p);
                     }
                }
                return ret;
            }

            
        ]]>
    </mx:Script>

     <mx:WebService id="ContestServiceFacadeBean"
                   useProxy="true"
                   destination="contest-facade">
        <mx:channelSet>
            <mx:ChannelSet id="contestfacadeChannelSet">
                <mx:channels>
                    <mx:SecureAMFChannel uri="{_blazedsendpoint}"/>
                </mx:channels>
            </mx:ChannelSet>
        </mx:channelSet>
                   <!--
        <mx:operation name="getPermissionsByUser"
                      resultFormat="object"
                      result="handlePermissionsByUser(event)"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('getPermissionsByUser: ' + event.fault.faultString);">
        </mx:operation>
        -->

        <mx:operation name="getPermissionsByProject"
                      resultFormat="object"
                      result="handlePermissionsByProject(event)"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('getPermissionsByProject: ' + event.fault.faultString);">
        </mx:operation>

        <mx:operation name="getSimpleContestDataByPID"
                      resultFormat="object"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('getSimpleContestDataByPID: ' + event.fault.faultString);">
        </mx:operation>

        <mx:operation name="addPermission"
                      resultFormat="object"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('addPermission: ' + event.fault.faultString);">
        </mx:operation>

        <mx:operation name="updatePermission"
                      resultFormat="object"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('updatePermission: ' + event.fault.faultString);">
        </mx:operation>

        <mx:operation name="deletePermission"
                      resultFormat="object"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('deletePermission: ' + event.fault.faultString);">
        </mx:operation>
        
        <mx:operation name="getSimpleProjectPermissionDataForUser"
                      resultFormat="object"
                      result="handleSimpleProjectPermissionDataForUser(event)"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('getSimpleProjectPermissionDataForUser: ' + event.fault.faultString);">
        </mx:operation>
        
        <mx:operation name="searchUser"
                      resultFormat="object"
                      result="handleSearchUser(event)"
                      fault="hideLoadingProgress(); Helper.showAlertMessage('searchUser: ' + event.fault.faultString);">
        </mx:operation>
    </mx:WebService>

    <mx:RemoteObject id="dataservice"
                     destination="remoteDataService"
                     endpoint="{_blazedsendpoint}"
                     fault="Helper.showAlertMessage('dataservice: ' + event.fault.faultString);">
        <mx:method name="getPassword"
                   result="processPassword(event)"
                   fault="Helper.showAlertMessage('getPassword: ' + event.fault.faultString);"/>

    </mx:RemoteObject>

    <mx:Binding source="model.selectedIndex"
                destination="selectedIndex"/>
    <mx:VBox id="content"
             width="100%"
             height="100%"
             includeInLayout="true"
             verticalScrollPolicy="off"
             paddingLeft="5"
             paddingRight="5">
        <com:SearchBar id="searchBar"
                       width="100%"
                       height="46"/>
        <mx:Canvas width="100%"
                   height="100%"
                   id="viewContainer">
            <mx:ViewStack width="100%"
                          height="100%"
                          id="stack">
                <com:NormalView id="normalViewer"
                                width="100%"
                                height="100%"/>
                <com:SearchResult id="searchResultViewer"
                                  width="100%"
                                  height="100%"/>
                <com:UserView id="userViewer"
                              width="100%"
                              height="100%"/>
            </mx:ViewStack>
        </mx:Canvas>
    </mx:VBox>
    <mx:Parallel id="moves"
                 effectEnd="handleEffectEnd()">
        <mx:Move id="move1"
                 xFrom="0"
                 xTo="{-stack.width / 2}"
                 duration="500"/>
        <mx:Move id="move2"
                 xFrom="{0}"
                 xTo="{stack.width / 2}"
                 duration="500"/>
    </mx:Parallel>
</tc:ProjectAdminWidgetCodeBehind>
